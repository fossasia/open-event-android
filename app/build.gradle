plugins {
    id "com.diffplug.gradle.spotless" version "3.16.0"
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'androidx.navigation.safeargs'

def STRIPE_API_TOKEN = System.getenv('STRIPE_API_TOKEN') ?: "YOUR_API_KEY"
def MAPBOX_KEY = System.getenv('MAPBOX_KEY') ?: "pk.eyJ1IjoiYW5nbWFzMSIsImEiOiJjanNqZDd0N2YxN2Q5NDNuNTBiaGt6eHZqIn0.BCrxjW6rP_OuOuGtbhVEQg"

android {
    compileSdkVersion versions.compileSdk
    defaultConfig {
        applicationId "com.eventyay.attendee"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode 3
        versionName "0.0.3a"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true
        manifestPlaceholders = [
                STRIPE_API_TOKEN: STRIPE_API_TOKEN
        ]
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", "DEFAULT_BASE_URL", '"https://api.eventyay.com/v1/"'
            buildConfigField "String", "MAPBOX_KEY", '"'+MAPBOX_KEY+'"'
        }
        debug {
            buildConfigField "String", "DEFAULT_BASE_URL", '"https://open-event-api-dev.herokuapp.com/v1/"'
            buildConfigField "String", "MAPBOX_KEY", '"'+MAPBOX_KEY+'"'
        }
    }

    flavorDimensions "default"

    productFlavors {
        fdroid {
            dimension "default"
        }

        playStore {
            dimension "default"
        }
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
    packagingOptions {
        pickFirst 'kotlin/**'
    }
    lintOptions {
        disable 'MissingTranslation'
    }
    androidExtensions {
        experimental = true
    }
}

spotless {
    kotlin {
        ktlint().userData(['android': 'true', 'color': 'true',
                           'max_line_length': '120', 'reporter': 'checkstyle'])
        target '**/*.kt'
    }
}

repositories {
    google()
    mavenCentral()
}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation libraries.multidex
    implementation libraries.appcompat
    implementation libraries.constraintLayout
    implementation libraries.cardView
    implementation libraries.recyclerView
    implementation libraries.material
    implementation libraries.browser
    implementation libraries.exifinterface
    implementation libraries.lifecycle_extensions
    implementation libraries.lifecycle_common
    implementation libraries.lifecycle_reactive
    implementation libraries.room_runtime
    implementation libraries.room_rxjava2
    kapt libraries.room_compiler
    testImplementation libraries.room_testing
    implementation libraries.preference
    implementation libraries.kotlin_stdlib

    // KTX
    implementation libraries.core_ktx
    implementation libraries.fragment_ktx
    implementation libraries.fragment_ktx
    implementation libraries.lifecycle_reactive_ktx

    // Koin
    implementation libraries.koin_android
    implementation libraries.koin_androidx_scope
    implementation libraries.koin_androidx_viewmodel

    // Location Play Service
    playStoreImplementation libraries.playServicesLocation

    //Smart Auth Play Service
    playStoreImplementation libraries.playServicesAuth

    // Timber
    implementation libraries.timber
    implementation libraries.threetenabp

    implementation libraries.jackson_module_kt
    implementation libraries.jsonapi_converter
    implementation libraries.logging_interceptor
    implementation libraries.retrofit2
    implementation libraries.converter_jackson

    // Cards Shimmer Animation
    implementation libraries.facebook_shimmer

    // RxJava
    implementation libraries.rxandroid
    implementation libraries.rxjava
    implementation libraries.rxjava2_adapter

    // Picasso
    implementation libraries.picasso

    // Stripe
    implementation libraries.stripe

    // QR Code
    implementation libraries.qrCode

    //Navigation
    implementation libraries.nav_fragment_ktx         // For Kotlin use navigation-fragment-ktx
    implementation libraries.nav_ui_ktx // For Kotlin use navigation-ui-ktx

    //Anko
    implementation libraries.anko_commons

    // Stetho
    debugImplementation libraries.stetho
    debugImplementation libraries.stetho_okhttp3
    releaseImplementation libraries.stetho_noop
    testImplementation libraries.stetho_noop

    //Mapbox SDK plugins
    implementation libraries.mapboxSdk

    testImplementation libraries.junit
    testImplementation libraries.mockk
    testImplementation libraries.threetenbp
    testImplementation libraries.koin_test
    testImplementation libraries.arch_core_test
    androidTestImplementation libraries.runner_test
    androidTestImplementation libraries.espresso
}
